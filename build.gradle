repositories {
	mavenCentral()
	jcenter()
}

defaultTasks 'build'

configurations {
	dbDriverLib { transitive = false }
	bouncyCastleLib { transitive = false }
	cloverLib { transitive = false }
	prometheus { transitive = false }
}

dependencies {
	cloverLib "javax.jms:javax.jms-api:2.0.1"
	
	dbDriverLib "com.microsoft.sqlserver:mssql-jdbc:8.4.1.jre8"
	dbDriverLib "com.mysql:mysql-connector-j:8.0.33"
	dbDriverLib "com.oracle.database.jdbc:ojdbc8:21.1.0.0"
	dbDriverLib "org.postgresql:postgresql:42.4.1"

	bouncyCastleLib "org.bouncycastle:bcprov-jdk15on:1.70"

	prometheus "io.prometheus.jmx:jmx_prometheus_javaagent:0.12.0"
}

task copyDbDriverLib(type: Copy) {
	from configurations.dbDriverLib
	into "public/var/dbdrivers"
}

task copyBouncyCastleLib(type: Copy) {
	from configurations.bouncyCastleLib
	into "public/var/bouncy-castle"
}

task copyCloverLib(type: Copy) {
	from configurations.cloverLib
	into "public/var/clover-lib"
}

task copyPrometheusJmxAgent(type: Copy) {
	from configurations.prometheus
	into "examples/kubernetes"
}

// Logging
tasks.withType(Copy) {
	doFirst { logger.lifecycle("Copying files into {}:", projectDir.toPath().relativize(destinationDir.toPath())) }
	eachFile { logger.lifecycle("* {}", it.file.name) }
}

task build(dependsOn: [copyDbDriverLib, copyCloverLib, copyBouncyCastleLib]) {
}